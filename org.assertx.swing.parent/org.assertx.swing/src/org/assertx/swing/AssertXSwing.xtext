grammar org.assertx.swing.AssertXSwing with org.eclipse.xtext.xbase.Xbase

generate assertXSwing "http://www.assertx.org/swing/AssertXSwing"

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

AXSTestCase:
	'testing' testedTypeRef=JvmParameterizedTypeReference ('as' fieldName=ID)?
	imports=XImportSection?
	members+=AXSMembers*
;

AXSMembers:
	AXSSettings | AXSTestMethod | AXSMatcher
;

AXSTestMethod:
	'test' name=STRING ('using' matchers+=AXSMatcherRef (',' matchers+=AXSMatcherRef)*)?
	block=XBlockExpression
;

AXSMatcherRef:
	reference=[AXSMatcher|ValidID] ('as' variableName=ValidID)?
;

AXSSettings:
	'settings' block=XBlockExpression
;

AXSMatcher:
	name=ValidID 'match' type=JvmParameterizedTypeReference matchingExpression=XBlockExpression
;