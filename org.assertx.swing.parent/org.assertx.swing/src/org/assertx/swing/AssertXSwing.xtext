grammar org.assertx.swing.AssertXSwing with org.eclipse.xtext.xbase.Xbase

generate assertXSwing "http://www.assertx.org/swing/AssertXSwing"

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/xtext/xbase/Xbase"

AXSTestCase:
	'testing' testedTypeRef=JvmParameterizedTypeReference ('as' fieldName=ID)?
	imports=XImportSection?
	members+=AXSMembers*
;

AXSMembers:
	AXSSettings | AXSTestMethod | AXSMatcher
;

AXSTestMethod:
	'test' name=STRING
	block=XBlockExpression
;

AXSMatcherRef returns XExpression:
	{AXSMatcherRef}
	'?' reference=[AXSMatcher|ValidID] '?'
;

@Override 
XPrimaryExpression returns XExpression:
	AXSMatcherRef | super
;

AXSSettings:
	'settings' block=XBlockExpression
;

AXSMatcher:
	'match' name=ValidID ':' type=JvmParameterizedTypeReference matchingExpression=XBlockExpression
;