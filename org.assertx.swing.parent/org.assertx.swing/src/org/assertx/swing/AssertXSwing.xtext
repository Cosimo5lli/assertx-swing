grammar org.assertx.swing.AssertXSwing with org.eclipse.xtext.xbase.Xbase

generate assertXSwing "http://www.assertx.org/swing/AssertXSwing"

AXSTestCase:
	'testing' testedTypeRef=JvmParameterizedTypeReference ('as' fieldName=ID)?
	imports=XImportSection?
	members+=AXSMembers*
;

AXSMembers:
	AXSSettings | AXSTestMethod | AXSMatcher
;

AXSTestMethod:
	'test' name=STRING ('using' matchers+=AXSMatcherRef (',' matchers+=AXSMatcherRef)*)?
	block=XBlockExpression
;

AXSMatcherRef:
	reference=JvmParameterizedTypeReference ('as' variableName=ValidID)?
;

AXSSettings:
	'settings' block=XBlockExpression
;

AXSMatcher:
	'matcher' name=ValidID type=JvmParameterizedTypeReference matchingLambda=XClosure
;